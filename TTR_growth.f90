!========================================================================================!
subroutine growth 
!----------------------------------------------------------------------------------------!
 use TTR_parameters
 use TTR_variables
 !---------------------------------------------------------------------------------------!
 implicit none
 !---------------------------------------------------------------------------------------!

   !-------------------------------------------------------------------------------------!
   ! Growth (G) of meristematic and structure dry matter for leaves
   !-------------------------------------------------------------------------------------!
   
   !-------------------------------------------------------------------------------------!
   ! Calculate the growth coefficients                       ([C]-1 [N]-1 [d]-1)
   !-------------------------------------------------------------------------------------!
   k_l_M     = k_l_M20 * f_T_air
   k_b_M     = k_b_M20 * f_T_air
   k_s_M     = k_s_M20 * f_T_air
   k_c_M     = k_c_M20 * f_T_soil
   k_f_M     = k_f_M20 * f_T_soil


   !-------------------------------------------------------------------------------------!
   ! Growth coefficient                                      ([d]-1)
   !-------------------------------------------------------------------------------------! 
   ! according to Mitscherlich's law of constant activity
   !-------------------------------------------------------------------------------------! 
   if (.not. LIEBIG) then
     mu_l_M    = k_l_M * C_l * N_l 
     mu_b_M    = k_b_M * C_b * N_b
     mu_s_M    = k_s_M * C_s * N_s
     mu_c_M    = k_c_M * C_c * N_c
     mu_f_M    = k_f_M * C_f * N_f

   !-------------------------------------------------------------------------------------!
   ! according to Liebig's law of the minimum
   !-------------------------------------------------------------------------------------! 
   else if (LIEBIG) then
     mu_l_M    = k_l_M * (C_l - C_l_threshold) * (N_l - N_l_threshold)
     mu_b_M    = k_b_M * (C_b - C_b_threshold) * (N_b - N_b_threshold)
     mu_s_M    = k_s_M * (C_s - C_s_threshold) * (N_s - N_s_threshold)
     mu_c_M    = k_c_M * (C_c - C_c_threshold) * (N_c - N_c_threshold)
     mu_f_M    = k_f_M * (C_f - C_f_threshold) * (N_f - N_f_threshold)
   end if   


   !-------------------------------------------------------------------------------------!   
   ! Calculate the surface area for branches, stems and coarse roots ([m]2 [stem]-1)
   !-------------------------------------------------------------------------------------! 
   A_b       = c_A_b * M_b_X**q_A_b
   A_s       = c_A_s * M_s_X**q_A_s
   A_c       = c_A_c * M_c_X**q_A_c


   !-------------------------------------------------------------------------------------!
   ! Calculate the potential meristem size                   ([kg X dm] [stem]-1)
   !-------------------------------------------------------------------------------------! 
   M_l_M_pot = C_l_M_pot * A_b * C_l * N_l
   M_b_M_pot = C_b_M_pot * A_b * C_b * N_b
   M_s_M_pot = C_s_M_pot * A_s * C_s * N_s
   M_c_M_pot = C_c_M_pot * A_c * C_c * N_c
   M_f_M_pot = C_f_M_pot * A_c * C_f * N_f


   !-------------------------------------------------------------------------------------!
   ! Determine growth allocation to meristem and structure   (unitless)
   !-------------------------------------------------------------------------------------! 
   z_l       = min ((M_l_M_pot / M_l_M - 1.0), 1.0)
   z_b       = min ((M_b_M_pot / M_b_M - 1.0), 1.0)
   z_s       = min ((M_s_M_pot / M_s_M - 1.0), 1.0)
   z_c       = min ((M_c_M_pot / M_c_M - 1.0), 1.0)
   z_f       = min ((M_f_M_pot / M_f_M - 1.0), 1.0)


   !-------------------------------------------------------------------------------------!
   ! Calculate growth of meristems                           ([kg X dm] [stem]-1 [d]-1)
   !-------------------------------------------------------------------------------------! 
   G_M_l_M   = mu_l_M * z_l * M_l_M
   G_M_b_M   = mu_b_M * z_b * M_b_M 
   G_M_s_M   = mu_s_M * z_s * M_s_M
   G_M_c_M   = mu_c_M * z_c * M_c_M
   G_M_f_M   = mu_f_M * z_f * M_f_M


   !-------------------------------------------------------------------------------------!   
   ! Calculate growth of structure                           ([kg X dm] [stem]-1 [d]-1)
   !-------------------------------------------------------------------------------------! 
   G_M_l_X   = mu_l_M * (1.0 - z_l) * M_l_M
   G_M_b_X   = mu_b_M * (1.0 - z_b) * M_b_M 
   G_M_s_X   = mu_s_M * (1.0 - z_s) * M_s_M
   G_M_c_X   = mu_c_M * (1.0 - z_c) * M_c_M 
   G_M_f_X   = mu_f_M * (1.0 - z_f) * M_f_M


   !-------------------------------------------------------------------------------------!    
   ! Calculate the growth of the leave area                  ([m]2 [stem]-1 [d]-1)
   !-------------------------------------------------------------------------------------! 
   c_SLA     = c_SLA_max * (1.0 - c_SLA_C * C_l)
   G_A_l     = c_SLA * G_M_l_X


   !-------------------------------------------------------------------------------------!
   ! Convert to growth per timestep                          ([kg X dm] [stem]-1 [tsp]-1)
   !-------------------------------------------------------------------------------------!
    G_M_l_M = G_M_l_M * dt
    G_M_b_M = G_M_b_M * dt
    G_M_s_M = G_M_s_M * dt
    G_M_c_M = G_M_c_M * dt
    G_M_f_M = G_M_f_M * dt
    G_M_l_X = G_M_l_X * dt
    G_M_b_X = G_M_b_X * dt
    G_M_s_X = G_M_s_X * dt
    G_M_c_X = G_M_c_X * dt
    G_M_f_X = G_M_f_X * dt
   !-------------------------------------------------------------------------------------!
    G_A_l   = G_A_l   * dt !                                 ([m]2 [stem]-1 [tsp]-1)
    
!----------------------------------------------------------------------------------------!
end subroutine growth
!========================================================================================!