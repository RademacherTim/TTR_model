!========================================================================================!
subroutine C_uptake 
!----------------------------------------------------------------------------------------!
 use TTR_parameters
 use TTR_variables
 !---------------------------------------------------------------------------------------!
 implicit none
 !---------------------------------------------------------------------------------------!
 
   !-------------------------------------------------------------------------------------!
   ! Light interception and photosynthesis
   !-------------------------------------------------------------------------------------!
   
   !-------------------------------------------------------------------------------------!
   ! Total plot leaf area index (LAI)                         ([m]2 leaf [m]2 ground)
   !-------------------------------------------------------------------------------------!
   LAI     = n_stems * A_l
   
   
   !-------------------------------------------------------------------------------------!
   ! Instantaneous light flux density                         ([J] [m]-2 ground [s]-1)
   !-------------------------------------------------------------------------------------!
   I       = J / h         
   
   
   !-------------------------------------------------------------------------------------!
   ! Maximal rate of photosynthesis at 20 degC                ([kg CO2] [m]-2 leaf [s]-1)
   !-------------------------------------------------------------------------------------!
   P_max20 = tau * C_CO2_air * N_l_tot / N_l_tot_s 


   !-------------------------------------------------------------------------------------!
   ! Max rate of photosynthesis at actual temperature         ([kg CO2] [m]-2 leaf [s]-1)
   !-------------------------------------------------------------------------------------!
   P_max   = P_max20 * f_T_air 
   
   
   !-------------------------------------------------------------------------------------!   
   ! Leaf photosynthetic efficiency                           ([kg CO2][J]-1)
   !-------------------------------------------------------------------------------------!
   alpha   = alpha_m * (1 - (beta / (tau * C_CO2_air))) 
   
   
   !-------------------------------------------------------------------------------------!
   ! Light flux density incident on leaves                    ([J] [m]-2 leaf [s]-1)
   !-------------------------------------------------------------------------------------!
   I_leaf  = (k * I) / (1 - Shi_leaf) * exp (-k * LAI)    


   ! For leaf-level photosynthesis resolve the following:
   ! O = theta * P_l**2 - P_l * (alpha * I_leaf + P_max) + alpha * P_max for P_l
   
   !-------------------------------------------------------------------------------------!
   ! Leaf-level photosynthesis                                ([kg CO2] [m]-2 leaf [s]-1)
   ! Explain exactly where this comes from
   !-------------------------------------------------------------------------------------!
   P_l     = 1 / (2 * theta)                                                             &
             * (alpha * I_leaf                                                           &
                + P_max                                                                  &
                - sqrt ((alpha * I_leaf + P_max)**2.0                                    &
                - 4 * theta * alpha * I_leaf * P_max)) 
   ! TTR Need to check this for units!
   
   !-------------------------------------------------------------------------------------!
   ! Canopy-level photosynthesis                              ([kg CO2] [m]-2 leaf [s]-1)
   ! Integrate across the canopy assuming ... (Thornley & Johnson, 1990, p. 249)
   ! P_c tends to be slightly underestimated (ibid.)
   !-------------------------------------------------------------------------------------!
   P_c     = P_l * ((1.0 - exp (-k * LAI)) / k)        
   
   
   !-------------------------------------------------------------------------------------!
   ! Conversion to uptake of carbon per stem per day          ([kg C] [stem]-1 [d]-1)
   !-------------------------------------------------------------------------------------!                     
   P_carb  = (12.0 / 44.0) * ((h * P_c) / (n_stems))   


   !-------------------------------------------------------------------------------------!
   ! Convert to uptake per timestep                           ([kg C] [stem]-1 [tsp]-1)
   !-------------------------------------------------------------------------------------!
   P_carb = P_carb * dt   
   
   
!----------------------------------------------------------------------------------------!
end subroutine C_uptake
!========================================================================================!